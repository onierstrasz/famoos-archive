"
I enacpsulate a FamoosWeb file.
Many of these files are HTML, and some ofthose are generated reference files.
"
Class {
	#name : #FamoosFile,
	#superclass : #Object,
	#instVars : [
		'fileReference',
		'hrefSea'
	],
	#category : #FamoosArchive
}

{ #category : #initialization }
FamoosFile class >> for: aFileReference [ 
	^ self new fileReference: aFileReference
]

{ #category : #accessing }
FamoosFile >> basename [
	^ self fileReference basename
]

{ #category : #accessing }
FamoosFile >> contents [
	^ self fileReference contents
]

{ #category : #accessing }
FamoosFile >> extension [
	^ self fileReference extension
]

{ #category : #accessing }
FamoosFile >> fileReference [
	^ fileReference
]

{ #category : #initialization }
FamoosFile >> fileReference: aFileReference [ 
	fileReference := aFileReference
]

{ #category : #accessing }
FamoosFile >> fileType [
	"^ self basename copyAfterLast: $."
	^ self extension
]

{ #category : #action }
FamoosFile >> gtActionWebBrowseFor: anAction [

	<gtAction>
	"self exists ifFalse: [ ^ anAction noAction ]."

	^ anAction button
		  icon: BrGlamorousIcons go;
		  tooltip: 'Open in OS';
		  action: [ WebBrowser openOn: self fileReference fullName ]
]

{ #category : #view }
FamoosFile >> gtContentsFor: aView [
	<gtView>
	self isFile ifFalse: [ ^ aView empty ].
	[ self fileReference readStreamDo: [ :s | s next: 1000 ] ]
		on: ZnInvalidUTF8
		do: [ ^ aView empty ].

	^ aView forward
		title: 'Contents';
		priority: 10;
		object: [ self fileReference ];
		view: #gtContentsFor:
]

{ #category : #view }
FamoosFile >> gtFigureFor: aView [
	<gtView>
	(self isFile
		and: [ ImageReadWriter allTypicalFileExtensions includes: self extension asLowercase ])
		ifFalse: [ ^ aView empty ].
	^ aView forward
		title: 'Figure';
		priority: 12;
		object: [ self fileReference ];
		view: #gtFigureFor:
]

{ #category : #view }
FamoosFile >> gtLinksFor: aView [
	<gtView>
	self isHtml ifFalse: [ ^ aView empty ].
	^ aView forward
		title: 'Links';
		priority: 30;
		object: [ self links ];
		view: #gtItemsFor:
]

{ #category : #view }
FamoosFile >> gtLinksWithStatusFor: aView [
	<gtView>
	self isHtml ifFalse: [ ^ aView empty ].
	^ aView columnedList
		title: 'Link status';
		priority: 40;
		items: [ self links ];
		actionUpdateButtonTooltip: 'Update item list';
		column: 'Index'
			text: [ :eachItem :eachIndex | eachIndex asRopedText foreground: Color gray ]
			width: 45;
		column: 'Item'
			text: [ :eachItem | eachItem gtDisplayText ]
			width: 400;
		column: 'Status'
			text: [ :eachItem | 
				(eachItem beginsWith: 'http://')
					ifTrue: [ FamoosUtility urlStatus: eachItem ]
					ifFalse: [ '' ] ]
]

{ #category : #view }
FamoosFile >> gtPathFor: aView [
	<gtView>
	^ aView forward
		title: 'Path';
		priority: 20;
		object: [ self fileReference ];
		view: #gtPathFor:
]

{ #category : #accessing }
FamoosFile >> hrefParser [
	"An island parser to extract hrefs.
	See: http://kursjan.github.io/petitparser2/scripting.html"

	^ HrefLinkParser new
]

{ #category : #testing }
FamoosFile >> isFile [
	^ self fileReference isFile
]

{ #category : #testing }
FamoosFile >> isHtml [
	^ self extension = 'html'
]

{ #category : #testing }
FamoosFile >> isInScgbib [
	^ Scgbib instance hasKey: self ref
]

{ #category : #testing }
FamoosFile >> isNotEmpty [
	"Needed for columnedTree view"

	^ false
]

{ #category : #accessing }
FamoosFile >> isRef [
	^ self isHtml
		and: [ self contents
				beginsWith: self refHeader ]
]

{ #category : #accessing }
FamoosFile >> links [
	self assert: self isHtml.
	^ self hrefParser parse: self contents
]

{ #category : #accessing }
FamoosFile >> localPath [
	"Path relative to the website folder. 
	Cache this if there are performance issues ..."

	^ self fileReference fullName withoutPrefix: FamoosWeb websiteFolder fullName
]

{ #category : #accessing }
FamoosFile >> printOn: aStream [
	self localPath printOn: aStream
]

{ #category : #accessing }
FamoosFile >> ref [
	"Extract the citation reference from the path."

	self assert: self isRef.
	^ ($/ split: self localPath) second
]

{ #category : #constant }
FamoosFile >> refHeader [
	^ '

<!-- THIS PAGE IS GENERATED BY ref2html -- DO NOT MODIFY MANUALLY-->

'
]
